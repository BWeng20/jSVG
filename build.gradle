plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = "io.github.bweng20"
version="1.1"

repositories {
    mavenCentral()
}

plugins.withType(JavaPlugin).configureEach {
    java {
        modularity.inferModulePath = true
        sourceCompatibility= JavaVersion.VERSION_1_9
        targetCompatibility= JavaVersion.VERSION_1_9
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

println "Configure $project.name, Gradle version $project.gradle.gradleVersion"

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    duplicatesStrategy = 'exclude'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

processResources {
    duplicatesStrategy = 'exclude'
}

dependencies {
    compileOnly 'org.apache.logging.log4j:log4j-api:2.17.2'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.17.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}


sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
        }
    }
}

jar {

    manifest {
        attributes 'Implementation-Title': 'A minimalistic SVG renderer',
                'Implementation-Version': project.version,
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Main-Class': 'com.bw.jtools.SVGViewer'
    }
    archiveBaseName = 'jSVG'
    includeEmptyDirs = false

    from "${rootProject.projectDir}/LICENSE"
    from "${rootProject.projectDir}/README.md"
}

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                // Place this two variable with the correct values in your
                // <Gradle-User-Home>/gradle.properties
                //  ossrhUsername=ID
                //  ossrhPassword=TOKEN
                //
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        }
    }
    publications {
        jSVG(MavenPublication) {
            from components.java

            artifacts = [jar, sourcesJar, javadocJar]

            pom {
                name = 'jSVG'
                artifactId = 'jSVG'
                packaging = 'jar'
                description = 'Draw icons and simple graphics from SVG that look great even on high-resolution screens.'
                url = 'https://github.com/BWeng20/jSVG'
                licenses {
                    license {
                        name = 'unlicense'
                        url = 'https://unlicense.org'
                    }
                }
                developers {
                    developer {
                        id = 'bweng20'
                        name = 'Bernd Wengenroth'
                        email = 'Bernd.Wengenrot@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com:BWeng20/jSVG.git'
                    developerConnection = 'https://github.com:BWeng20/jSVG.git'
                    url = 'https://github.com/BWeng20/jSVG'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.jSVG
}